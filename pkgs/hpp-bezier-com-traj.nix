{
  lib,
  fetchFromGitHub,
  stdenv,

  pythonSupport ? false,
  python3Packages,

  # propagatedBuildInputs
  cddlib,
  clp,
  glpk,
  hpp-centroidal-dynamics,
  jrl-cmakemodules,
  ndcurves,
  qpoases,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "hpp-bezier-com-traj";
  version = "7.0.0";

  src = fetchFromGitHub {
    owner = "humanoid-path-planner";
    repo = "hpp-bezier-com-traj";
    tag = "v${finalAttrs.version}";
    hash = "sha256-vsYs7F1OumIbKMUTqAIQG2sBjaeKrDRJyxk3pxUWxRo=";
  };

  outputs = [
    "out"
    "doc"
  ];

  strictDeps = true;

  nativeBuildInputs =
    jrl-cmakemodules.doxygenNativeInputs
    ++ lib.optionals pythonSupport [
      python3Packages.python
      python3Packages.pythonImportsCheckHook
    ];

  buildInputs = [ jrl-cmakemodules ];

  propagatedBuildInputs = [
    cddlib
    clp
    glpk
    qpoases
  ]
  ++ lib.optionals pythonSupport [
    python3Packages.hpp-centroidal-dynamics
    python3Packages.ndcurves
  ]
  ++ lib.optionals (!pythonSupport) [
    hpp-centroidal-dynamics
    ndcurves
  ];

  cmakeFlags =
    jrl-cmakemodules.doxygenCmakeFlags
    ++ [
      (lib.cmakeBool "BUILD_PYTHON_INTERFACE" pythonSupport)
      (lib.cmakeBool "USE_GLPK" true)
    ]
    ++ lib.optionals stdenv.targetPlatform.isDarwin [
      (lib.cmakeFeature "CMAKE_CTEST_ARGUMENTS" "--exclude-regex;'transition'")
    ];

  doCheck = true;

  pythonImportsCheck = [ "hpp_bezier_com_traj" ];

  meta = {
    description = "Multi contact trajectory generation for the COM using Bezier curves";
    homepage = "https://github.com/humanoid-path-planner/hpp-bezier-com-traj";
    changelog = "https://github.com/humanoid-path-planner/hpp-bezier-com-traj/releases/tag/${finalAttrs.src.tag}";
    license = lib.licenses.bsd2;
    maintainers = [ lib.maintainers.nim65s ];
    platforms = lib.platforms.unix;
  };
})
